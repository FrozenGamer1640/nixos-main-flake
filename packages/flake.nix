{
  description = "Packages and overlays management sub-flake";

  inputs = {
    nixpkgs = {
      type = "github";
      owner = "nixos";
      repo = "nixpkgs";
      ref = "nixos-25.11";
    };
    nixpkgs-unstable = {
      type = "github";
      owner = "nixos";
      repo = "nixpkgs";
      ref = "nixos-unstable";
    };
    home-manager = {
      type = "github";
      owner = "nix-community";
      repo = "home-manager";
      ref = "release-25.11";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    stylix = {
      type = "github";
      owner = "nix-community";
      repo = "stylix";
      ref = "release-25.11";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    nixvim = {
      type = "github";
      owner = "nix-community";
      repo = "nixvim";
      ref = "nixos-25.11";
    };
    hyprland = {
      type = "github";
      owner = "hyprwm";
      repo = "Hyprland";
      ref = "nix";
      inputs.nixpkgs.follows = "nixpkgs-unstable";
    };
    vieb = {
      type = "github";
      owner = "tejing1";
      repo = "vieb-nix";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    copyparty = {
      type = "github";
      owner = "9001";
      repo = "copyparty";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    seanime = {
      type = "github";
      owner = "rishabh5321";
      repo = "seanime-flake";
      inputs.nixpkgs.follows = "nixpkgs";
    };
  };

  outputs =
    inputs@{
      self,
      nixpkgs,
      nixpkgs-unstable,
      ...
    }:
    {
      inherit nixpkgs nixpkgs-unstable;
      inherit (inputs) home-manager stylix;

      # Add all the systems your flake will support
      systems = [
        "x86_64-linux"
      ];
      eachSystem = nixpkgs.lib.genAttrs self.systems; # Extracted from the Hyprland's flake btw

      forAllSystems =
        nixpkgs-input: function: self.eachSystem (system: function (self.withSystem system nixpkgs-input));

      withSystem =
        system: nixpkgs:
        (import nixpkgs {
          inherit system;
          config.allowUnfree = true; # You migth wanna delete this, but I really hate defining predications everytime
        });

      withOverlays = overlays: { nixpkgs.overlays = overlays; };
      withAllOverlays = self.withOverlays (builtins.attrValues self.overlays);

      overlays = {
        inherit (inputs.hyprland.overlays) hyprland-packages;
        copyparty = inputs.copyparty.overlays.default;
        default = final: prev: {
          osu-resources = final.callPackage ./osu-resources.nix { };
          zed-color-highlight = final.callPackage ./zed-color-lsp.nix { };
          vieb = (inputs.vieb.packagesFunc final).vieb;
        };
      };

      nixosModules = {
        inherit (inputs.stylix.nixosModules) stylix;
        inherit (inputs.nixvim.nixosModules) nixvim;
        copyparty = inputs.copyparty.nixosModules.default;
      }
      // (import ../modules/nixos self.nixpkgs.lib);

      homeModules = {
        inherit (inputs.stylix.homeModules) stylix;
        inherit (inputs.seanime.nixosModules) seanime;
      }
      // (import ../modules/home self.nixpkgs.lib);

      stylixModules = (import ../modules/stylix self.nixpkgs.lib);
    };
}

